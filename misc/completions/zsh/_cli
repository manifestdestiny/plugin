#compdef plugin
# vim: ft=zsh

_plugin () {
    local -a _1st_arguments
    _1st_arguments=(
    'configure:Edit config file'
    'edit:Edit cli-plugin-plugin file'
    'exec:Run the selected commands'
    'help:Help about any command'
    'list:Show all cli-plugin-plugins'
    'new:Create a new cli-plugin-plugin'
    'search:Search cli-plugin-plugins'
    'sync:Sync cli-plugin-plugins'
    'version:Print the version number'
    )

    _arguments \
        '(--help)--help[show this help message]' \
        '(--config)--config=[config file (default is $HOME/.config/plugin/config.toml)]' \
        '(--debug)--debug[debug mode]' \
        '*:: :->subcmds' \
        && return 0

    if (( CURRENT == 1 )); then
        _describe -t commands "plugin subcommand" _1st_arguments
        return
    fi

    case "$words[1]" in
        ("configure"|"edit"|"version")
            _arguments \
                '(- :)'{-h,--help}'[Show this help and exit]' \
                && return 0
            ;;
        ("exec")
            _arguments \
                '(- :)'{-h,--help}'[Show this help and exit]' \
                '(--color)--color[Enable colorized output (only fzf)]' \
                '(-q --query)'{-q,--query}'=[Initial value for query]' \
                && return 0
            ;;
        ("list")
            _arguments \
                '(- :)'{-h,--help}'[Show this help and exit]' \
                '(--oneline)--oneline[Display cli-plugin-plugins in one line]' \
                && return 0
            ;;
        ("new")
            _arguments \
                '(- :)'{-h,--help}'[Show this help and exit]' \
                '(-t --tag)'{-t,--tag}'=[Display tag prompt (delimiter: space)]' \
                && return 0
            ;;
        ("search")
            _arguments \
                '(- :)'{-h,--help}'[Show this help and exit]' \
                '(--color)--color[Enable colorized output (only fzf)]' \
                '(-d --delimiter)'{-d,--delimiter}'[Use delim as the command delimiter character (default "; ")]' \
                '(-q --query)'{-q,--query}'=[Initial value for query]' \
                && return 0
            ;;
        ("sync")
            _arguments \
                '(- :)'{-h,--help}'[Show this help and exit]' \
                '(-u --upload)'{-u,--upload}'[Upload cli-plugin-plugins to gist]' \
                && return 0
            ;;
        ("help")
            _values 'help message' ${_1st_arguments[@]%:*} && return 0
            ;;
    esac
}

_plugin "$@"
